/*******************************************************************************
 * Copyright (c) 2013, 2015 Willink Transformations and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     E.D.Willink - initial API and implementation
 *     Adolfo Sanchez-Barbudo Herrera (University of York) - Lookup Environment/Visitor
 *
 *******************************************************************************/
module SubGenerateAutoASModel

import org.eclipse.ocl.examples.build.utilities.GenerateModel
import org.eclipse.ocl.examples.build.xtend.GenerateCSVisitors
import org.eclipse.ocl.examples.build.xtend.GenerateAutoLookupInfrastructureXtend 
import org.eclipse.emf.ecore.resource.ResourceSet

/**
 * A ResourceSet that may be shared by workflow components.
 */
var ResourceSet resourceSet

/**
 * The name of the project hosting the generated models; e.g. "org.eclipse.ocl.xtext.essentialocl"
 */
var projectName

/**
 * The path inside the projectName to the Complete OCL file which contains the name resolution description; 
 * (e.g. "model/NameResolution.ocl"). 
 */
var lookupFilePath

/**
 * The name of the generated model (the package name in the Ecore model); e.g. "essentialoclcs"
 */
var modelName

/**
 * The prefix of generated model classes; e.g. "EssentialOCL"
 */
var projectPrefix

/**
 * The name of the inherited project hosting the generated models; e.g. "org.eclipse.ocl.xtext.essentialocl"; "" for a base project.
 */
var superProjectName = ""

/**
 * The name of the inherited model (the package name in the Ecore model); e.g. "basecs"; "" for a base project.
 */
var superModelName = ""

/**
 * The prefix of the inherited project hosting the generated models; e.g. "EssentialOCL", "" for a base visitor.
 */
var superProjectPrefix = ""

/*
 * The prefix of the inherited project hosting the base generate models; e.g. "Pivot", "" for a base visitor.
 */
var baseProjectPrefix = ""

/* The name of the visitor package that is base of the project containing  the common visitor Visitor/Visitable interfances. 
 * (e.g. "org.eclipse.pivot.ocl.util"). "" for a base visitor
 */
var baseVisitorPackage = ""
 
/**
 * (Optional) The package for generated model interfaces; default: "${projectName}.${modelName}".
 */
var modelPackageName = "${projectName}.${modelName}"

/**
 * (Optional) The super package for generated model interfaces; default: "${superProjectName}.${superModelName}".
 */
var superModelPackageName = "${superProjectName}.${superModelName}"

/**
 * (Optional) The package for generated lookup artefacts; default: "${modelPackageName}.lookup.util".
 */
var lookupPackageName = "${modelPackageName}.lookup"

/**
 * (Optional) The super package for generated lookup artefacts; default: "${superModelPackageName}.lookup.util".
 */
var superlookupPackageName = "${superModelPackageName}.lookup"

/**
 * (Optional) The project-relative genmodel file; default: "model/${projectPrefix}.genmodel".
 */
var genModelFile = "model/${projectPrefix}.genmodel"

/**
 * (Optional) The Java source folder for generated packages; default: "emf-gen".
 */
var javaFolder = "emf-gen"

/**
 * (Optional) The package of the visitor; default: "${projectName}.util".
 */
var visitorPackageName = "${modelPackageName}.util"

/**
 * (Optional) The class of the visitor; default: "${projectPrefix}Visitor".
 */
var visitorClassName = "${projectPrefix}Visitor"

/**
 * (Optional) The package of the inherited visitor; default: "${superProjectName}.util". "" for a base visitor.
 */
var superVisitorPackageName = "${superModelPackageName}.util"

/**
 * (Optional) The class of the inherited visitor; default: "${superProjectPrefix}Visitor". "" for a base visitor.
 */
var superVisitorClassName = "${superProjectPrefix}Visitor"

/**
 * (Optional) The package of the visitable interface class; default: "org.eclipse.ocl.pivot.util"
 */
var visitablePackageName = "${visitorPackageName}"

/**
 * (Optional) The name of the visitable interface class; default: "Visitable"
 */
var visitableClassName = "Visitable"

/**
 * (Optional) The workspace-relative name of the gen model file; default: "/${projectName}/${genModelFile}"
 */
//var genModel = "/${projectName}/${genModelFile}"

/**
 *	Generate a Concrete Syntax Model and its visitors as used by the Xtext editors.
 */
Workflow {
    component = GenerateAutoLookupInfrastructureXtend { resourceSet = resourceSet skipOnErrors = true
    	projectName = projectName
    	projectPrefix = projectPrefix
    	superProjectName = superProjectName
    	superProjectPrefix = superProjectPrefix
    	lookupFilePath = lookupFilePath
    	genModelFile = genModelFile
    	javaFolder = javaFolder
    	modelPackageName = modelPackageName
    	visitorPackageName = visitorPackageName
    	visitorClassName = visitorClassName
    	superVisitorPackageName = superVisitorPackageName
    	superVisitorClassName = superVisitorClassName
    	visitablePackageName = visitablePackageName
    	visitableClassName = visitableClassName
    	baseProjectPrefix = baseProjectPrefix
    	baseVisitorPackage = baseVisitorPackage
    	lookupPackageName = lookupPackageName
    	superLookupPackageName = superlookupPackageName
    }
}
